apply from: "$rootDir/build-support/emp3-gradle-plugin/src/main/resources/android-app.gradle"

repositories {
    maven {
        url 'https://bintray.com/nasaworldwind/maven/WorldWindAndroid/'
    }
}

dependencies {
    implementation   ("com.android.support:support-annotations")
    compileOnly project (":emp3-android-sdk")
    implementation      ("${cmapi_group_name}:${cmapi_jvm_artifact_name}:${version_cmapi}")
    compileOnly project (":geolib")
    compileOnly      (group: 'mil.army.missioncommand', name: 'mil-sym-android-renderer', ext: 'jar') { transitive = true }
    implementation   ("gov.nasa.worldwind.android:worldwind:${version_nasa_worldwind_sdk}@aar")
}

android {
    def engine_version = dependencyManagement.importedProperties['version_nasa_worldwind_sdk']

    defaultConfig {
        applicationId "mil.emp3.worldwind"
    }

    // Add map engine id and version information to BuildConfig to be retrieved by About class.
    buildTypes.each {
        it.buildConfigField "String", "Implementation_Map_Engine_Id", "\"worldwind_android_sdk ${engine_version}\""
    }
}

build.doLast { task ->
    def sourceDir = "$projectDir/build/outputs/apk/release/"
    def fileName = "mapengine-worldwind-apk-release-unsigned.apk"

    def checksumTarget = sourceDir + fileName
    ant.checksum file: checksumTarget, algorithm: "MD5", fileExt: ".md5"
    ant.checksum file: checksumTarget, algorithm: "SHA-256", fileExt: ".sha256"
}

task releaseChecksum {
    doLast {
        def sourceDir = "$projectDir/build/outputs/apk/release/"
        def fileName = "mapengine-worldwind-apk-release.apk"

        def checksumTarget = sourceDir + fileName
        ant.checksum file: checksumTarget, algorithm: "MD5", fileExt: ".md5"
        ant.checksum file: checksumTarget, algorithm: "SHA-256", fileExt: ".sha256"
    }
}
