apply plugin: 'com.android.library'
apply plugin: 'jacoco-android'

// versionCode is an integer and must increase with each release.
def vc = "$project.version".tokenize('.');
def major = vc[0].toInteger() * 1000000
def minor = vc[1].toInteger() * 1000
def patch = vc[2].tokenize('-')[0].toInteger()

android {
    compileSdkVersion android_compileSdkVersion
    buildToolsVersion android_buildToolsVersion

    defaultConfig {
        minSdkVersion android_minSdkVersion
        targetSdkVersion android_targetSdkVersion
        versionCode (major + minor + patch)
        versionName "${project.version}"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField "String", "Implementation_Id", "\"${project.name}\""
        it.buildConfigField "String", "Implementation_Vendor", "\"mil.army.missioncommand\""
        it.buildConfigField "String", "Implementation_Title", "\"EMP3 Android ${project.version}\""
        it.buildConfigField "String", "Implementation_Version", "\"${project.revision}\""
        it.buildConfigField "String", "CMAPI_Version", "\"${version_cmapi}\""
        it.buildConfigField "String", "SEC Renderer Version", "\"${version_renderer}\""
    }
    compileOptions {
        sourceCompatibility rootProject.sourceCompatibility
        targetCompatibility rootProject.targetCompatibility
    }
    lintOptions {
        abortOnError false
    }
}

