buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'https://maven.google.com'
            // Alternative URL is 'https://dl.google.com/dl/android/maven2/'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-beta3'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:5.1.1'
        classpath 'org.ajoberstar:grgit:1.7.2'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.5.0'
    }
}

ext {
    empUsername = System.getenv("CI_USERNAME") ?: ""
    empPassword = System.getenv("CI_PASSWORD") ?: ""

    bintrayUser = System.getenv("BINTRAY_USER") ?: project.hasProperty('bintrayUser') ? bintrayUser : ''
    bintrayKey  = System.getenv("BINTRAY_KEY")  ?: project.hasProperty('bintrayKey') ? bintrayKey : ''

    description = "Extensible Map Platform (EMP)"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8


    git = org.ajoberstar.grgit.Grgit.open(file('.')) // Open the Git repository in the current directory.
    revision = git.head().id                         // Get commitId of HEAD.
}

allprojects {
    apply from: "$rootDir/build-support/emp3-gradle-plugin/src/main/resources/common.gradle"
}

def publishableModules() {
    [
          project(":emp3-android-sdk")
        , project(":emp3-android-sdk-apk")
        , project(":emp3-android-sdk-view")
        , project(":emp3-android-sdk-core")
        , project(":emp3-gradle-plugin")
        , project(":mapengine-worldwind")
        , project(":google-maps-android-cmgn")
          , project (":cmapi")

    ]
}

configure(publishableModules()) {
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.jfrog.artifactory'
    apply from: "${rootDir}/gradle/bintray.gradle"
}

task bintray {
    group 'Publishing'
    description 'Publish artifacts to Bintray'

    dependsOn publishableModules().bintrayUpload
}
