apply from: "$rootDir/build-support/emp3-gradle-plugin/src/main/resources/android-app.gradle"
apply plugin: 'maven-publish'

dependencies {
    compile project (":emp3-android-sdk")
    //compile project (":service:mirrorcache:service-mirrorcache-api")
    //compile project (":service:mirrorcache:service-mirrorcache-mirrorables")

    compile         ("com.android.support:appcompat-v7")

    testCompile     ("junit:junit")
}

android {
    defaultConfig {
        applicationId "mil.emp3.emp3_android_sdk_apk"
    }
}
build.doLast { task ->
    def sourceDir = "$projectDir/../sdk-apk/build/outputs/apk/release/"
    def fileName = "emp3-android-sdk-apk-release-unsigned.apk"

    def checksumTarget = sourceDir + fileName
    ant.checksum file: checksumTarget, algorithm: "MD5", fileExt: ".md5"
    ant.checksum file: checksumTarget, algorithm: "SHA-256", fileExt: ".sha256"
}

publishing {
    publications {
        nebula(MavenPublication) {

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // inject the compile-time dependencies
                // TODO: this handles basic case;
                //       need to find existing plugin and/or add more robustness
                configurations.compile.allDependencies.each {
                    if (it.name != 'unspecified') {

                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group == 'emp3-android' ? project.ext.group : it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        if (it.version != null) {
                            //def depVer = project.dependencyManagement.dependencyManagementContainer.globalDependencyManagement.versions["$it.group:$it.name"]
                            dependencyNode.appendNode('version', it.version == 'unspecified' ? rootProject.ext.version : it.version)
                        }
                    }
                }
            }

            artifact("$buildDir//outputs/apk/emp3-android-sdk-apk-release-unsigned.apk") {
                extension "apk"
            }

            artifact("$buildDir/outputs/apk/emp3-android-sdk-apk-release-unsigned.apk.sha256") {
                extension "apk.sha256"
            }

            artifact("$buildDir/outputs/apk/emp3-android-sdk-apk-release-unsigned.apk.md5") {
                extension "apk.md5"
            }

        }
    }
}
