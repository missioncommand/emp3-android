apply from: "$rootDir/build-support/emp3-gradle-plugin/src/main/resources/android-lib.gradle"
apply plugin: 'maven-publish'

dependencies {
    implementation project     (":emp3-android-sdk")
    //implementation project     (":service:mirrorcache:service-mirrorcache-api")
    //implementation project     (":service:mirrorcache:service-mirrorcache-mirrorables")
    implementation             ("com.android.support:appcompat-v7")
    implementation             ("mil.army.missioncommand:mil-sym-android-renderer")

    provided project (":cmapi")
    testImplementation project (":emp3-android-sdk-core-aar")
    testImplementation         ("junit:junit:4.12")
    //testImplementation         ("org.mockito:mockito-core")
    testImplementation         ("org.powermock:powermock-api-mockito:1.6.2") { // PowerMockito is required to mock static methods like Logger.log
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }
    testImplementation         ("org.powermock:powermock-module-junit4:1.6.2") {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }

    testImplementation ("org.robolectric:robolectric")
}


publishing {
    publications {
        nebula(MavenPublication) {
            artifact "$buildDir/outputs/aar/${project.name}-release.aar"

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // inject the compile-time dependencies
                // TODO: this handles basic case;
                //       need to find existing plugin and/or add more robustness
                configurations.compile.allDependencies.each {
                    if (it.name != 'unspecified') {

                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group == 'emp3-android-sdk-Main' ? project.ext.group : it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        if (it.version != null) {
                            //def depVer = project.dependencyManagement.dependencyManagementContainer.globalDependencyManagement.versions["$it.group:$it.name"]
                            dependencyNode.appendNode('version', it.version == 'unspecified' ? rootProject.ext.version : it.version)
                        }
                    }
                }
            }
        }
    }
}

android {
    // Add sec renderer id and version information to BuildConfig to be retrieved by About class.
    buildTypes.each {
        it.buildConfigField "String", "Implementation_Sec_Renderer_Id", "\"sec.web.renderer:mil-sym-android-renderer ${version_renderer}\""
        it.buildConfigField "String", "Implementation_Sec_Cmapi_Id", "\"org.cmapi.primitives:common-map-geospatial-notation ${version_cmapi}\""
    }

    lintOptions {
        abortOnError false
    }
}
